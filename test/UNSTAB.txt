AUTO
MODE 4
INPUT "CPU NAME ";CPU$
INPUT "DATE CODE ";DATE$
INPUT "CLOCK SPEED ";SPEED$
FOR F=0 TO 1
  FOR INSTR=0 TO 3
    FOR CF=0 TO 1
      code%=&E000
      OSWRCH=&FFEE
      FOR I%=0 TO 2 STEP 2
        P%=code%
        [OPT I%
        .test
        \ SET Y/X FLAGS
        LD DE,&0000
        .loop
        PUSH DE
        PUSH DE
        POP AF
        NOP
        NOP
        POP AF
        ]
        IF INSTR=1 THEN [OPT I%:NOP:]
        IF INSTR=2 THEN [OPT I%:LD R,A:]
        IF INSTR=3 THEN [OPT I%:XOR &00:]
        IF CF=0 THEN [OPT I%:CCF:]
        IF CF=1 THEN [OPT I%:SCF:]
        [OPT I%
        \ SAVE FLAGS
        PUSH AF
        POP HL
        LD A,L
        ]
        IF F=1 THEN [OPT I%:RRCA:RRCA:]
        [OPT I%
        AND &08
        JR NZ skip
        LD A,25
        CALL OSWRCH
        LD A,71
        CALL OSWRCH
        LD L,D
        LD H,&00
        ADD HL,HL
        ADD HL,HL
        LD A,L
        CALL OSWRCH
        LD A,H
        CALL OSWRCH
        LD L,E
        LD H,&00
        ADD HL,HL
        ADD HL,HL
        LD A,L
        CALL OSWRCH
        LD A,H
        CALL OSWRCH
        .skip
        INC E
        JR NZ,loop
        INC D
        JR NZ,loop
        RET
        ]
        NEXT
      MODE 4
      MOVE 0,0
      MOVE 1023,0
      PLOT 85,0,1023
      PLOT 85,1023,1023
      VDU 23,1,0;0;0;0;
      MOVE 1100,250
      DRAW 1100,100
      DRAW 1250,100
      MOVE 1100,250:DRAW 1080,230
      MOVE 1100,250:DRAW 1120,230
      MOVE 1250,100:DRAW 1230,120
      MOVE 1250,100:DRAW 1230,80
      VDU 31,33,26,ASC"F"
      VDU 31,36,29,ASC"A"
      VDU 28,32,31,39,0,30
      PRINT "CPU:"'CPU$
      VDU 31,0,3
      PRINT "DATE:"'DATE$'
      PRINT "CLOCK:"'SPEED$'
      PRINT "TESTING:";
      IF INSTR=0 PRINT "POP AF"
      IF INSTR=1 PRINT "POP AF"'"NOP"
      IF INSTR=2 PRINT "POP AF"'"LD R,A"
      IF INSTR=3 PRINT "POP AF"'"XOR &00"
      IF CF=0 PRINT "CCF" ELSE PRINT "SCF"
      PRINT "PUSH AF"'
      PRINT "GRAPH:"'CHR$(ASC("X")+F);" FLAG"'
      PRINT "X AXIS:"'"A:00-FF"'
      PRINT "Y AXIS:"'"F:00-FF"'
      CALL test
      REM TAKE A SCREEN SHOT
      *MOTOR 1
      T=TIME
      REPEAT UNTIL TIME>T+20
      *MOTOR 0
      TIME=T
      REPEAT UNTIL TIME>T+100
      NEXT
    NEXT
  NEXT
